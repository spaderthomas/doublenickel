@ECHO OFF

:: Import the DN build script, which defines any framework paths and provides some utilities
SET "DN_DIR=..\.."
SET "DN_BUILD_TOOLS=%DN_DIR%\build\dn.bat"
CALL %DN_BUILD_TOOLS%

CALL %DN_BUILD_TOOLS% :DN_NORMALIZE_PATH "%DN_DIR%" DN_DIR
CALL %DN_BUILD_TOOLS% :DN_NORMALIZE_PATH "%DN_BUILD_TOOLS%" DN_BUILD_TOOLS
CALL %DN_BUILD_TOOLS% :DN_NORMALIZE_PATH "%DN_DIR%\external\build\" DN_EXTERNAL_BUILD_DIR
CALL %DN_BUILD_TOOLS% :DN_NORMALIZE_PATH "%DN_EXTERNAL_BUILD_DIR%\glfw" DN_GLFW_DIR
CALL %DN_BUILD_TOOLS% :DN_NORMALIZE_PATH "%DN_GLFW_DIR%\build" DN_GLFW_BUILD_DIR
CALL %DN_BUILD_TOOLS% :DN_NORMALIZE_PATH "%DN_EXTERNAL_BUILD_DIR%\cimgui" DN_CIMGUI_DIR
CALL %DN_BUILD_TOOLS% :DN_NORMALIZE_PATH "%DN_CIMGUI_DIR%\build" DN_CIMGUI_BUILD_DIR

SET "DN_REDIRECT_OUTPUT=>NUL 2>&1"

:: Call the correct target (or :BUILD by default)
IF NOT "%~1"=="" (SHIFT & GOTO %~1)

:ALL
  CALL :GLFW
  CALL :CIMGUI

  EXIT /B

:CLEAN
  RMDIR /S /Q %DN_GLFW_BUILD_DIR%
  RMDIR /S /Q %DN_CIMGUI_BUILD_DIR%
  EXIT /B

:GLFW
  CALL %DN_BUILD_TOOLS% :DN_ECHO_TARGET "GLFW"
  CALL :GLFW_EX Debug
  CALL :GLFW_EX Release
  EXIT /B

:GLFW_EX
  SET "GLFW_BUILD_TYPE=%~1"

  CALL %DN_BUILD_TOOLS% :DN_ECHO_INFO "Build GLFW [%GLFW_BUILD_TYPE%]"

  CALL %DN_BUILD_TOOLS% :DN_NORMALIZE_PATH "%DN_GLFW_BUILD_DIR%\src\%GLFW_BUILD_TYPE%" DN_GLFW_BINARY_DIR
  CALL %DN_BUILD_TOOLS% :DN_NORMALIZE_PATH "%DN_GLFW_BINARY_DIR%\glfw3.dll" DN_GLFW_OUTPUT_DLL
  CALL %DN_BUILD_TOOLS% :DN_NORMALIZE_PATH "%DN_GLFW_BINARY_DIR%\glfw3dll.lib" DN_GLFW_OUTPUT_LIB
  CALL %DN_BUILD_TOOLS% :DN_NORMALIZE_PATH "%DN_GLFW_BINARY_DIR%\glfw3.pdb" DN_GLFW_OUTPUT_PDB

  MKDIR %DN_GLFW_BUILD_DIR% %DN_REDIRECT_OUTPUT%

  SET "GLFW_CMAKE_GENERATOR_FLAGS=-D BUILD_SHARED_LIBS=1 -D USE_MSVC_RUNTIME_LIBRARY_DLL=1 -D GLFW_BUILD_EXAMPLES=0 -D GLFW_BUILD_TESTS=0 -D GLFW_BUILD_DOCS=0"

  @ECHO ON
  cmake -S %DN_GLFW_DIR% -B %DN_GLFW_BUILD_DIR% %GLFW_CMAKE_GENERATOR_FLAGS%
  cmake --build %DN_GLFW_BUILD_DIR% %GLFW_CMAKE_BUILD_FLAGS% --config %GLFW_BUILD_TYPE%
  @ECHO OFF

  CALL %DN_BUILD_TOOLS% :DN_ECHO_RESULT

  CALL %DN_BUILD_TOOLS% :DN_ECHO_INFO "Copy GLFW [%GLFW_BUILD_TYPE%]"
  CALL %DN_BUILD_TOOLS% :DN_REPLACE_FILE %DN_DLL_GLFW% %DN_GLFW_OUTPUT_DLL%
  CALL %DN_BUILD_TOOLS% :DN_REPLACE_FILE %DN_LIB_GLFW% %DN_GLFW_OUTPUT_LIB%

  IF %GLFW_BUILD_TYPE%==Debug (
    CALL %DN_BUILD_TOOLS% :DN_REPLACE_FILE %DN_PDB_GLFW% %DN_GLFW_OUTPUT_PDB%
  )

  EXIT /B

:CIMGUI
  CALL %DN_BUILD_TOOLS% :DN_ECHO_TARGET "CIMGUI"

  CALL %DN_BUILD_TOOLS% :DN_NORMALIZE_PATH "%DN_CIMGUI_BUILD_DIR%\intermediate" DN_CIMGUI_INTERMEDIATE_DIR
  CALL %DN_BUILD_TOOLS% :DN_NORMALIZE_PATH "%DN_CIMGUI_INTERMEDIATE_DIR%\" DN_CIMGUI_OBJECT_DIR
  CALL %DN_BUILD_TOOLS% :DN_NORMALIZE_PATH "%DN_CIMGUI_INTERMEDIATE_DIR%\" DN_CIMGUI_PDB_DIR
  
  SET "DN_CIMGUI_SOURCES=%DN_CIMGUI_DIR%\cimgui.cpp %DN_CIMGUI_DIR%\imgui\imgui.cpp %DN_CIMGUI_DIR%\imgui\imgui_draw.cpp %DN_CIMGUI_DIR%\imgui\imgui_demo.cpp %DN_CIMGUI_DIR%\imgui\imgui_tables.cpp %DN_CIMGUI_DIR%\imgui\imgui_widgets.cpp %DN_CIMGUI_DIR%\imgui\backends\imgui_impl_opengl3.cpp %DN_CIMGUI_DIR%\imgui\backends\imgui_impl_glfw.cpp"
  SET "DN_CIMGUI_H_OUTPUT=%DN_CIMGUI_DIR%\cimgui.h"
  SET "DN_CIMGUI_H=%DN_IMGUI%\cimgui.h"
  SET "DN_CIMGUI_IMPL_H_OUTPUT=%DN_CIMGUI_DIR%\cimgui_impl.h"
  SET "DN_CIMGUI_IMPL_H=%DN_IMGUI%\cimgui_impl.h"
  SET "DN_CIMGUI_OUTPUT_DLL=%DN_CIMGUI_BUILD_DIR%\cimgui.dll"
  SET "DN_CIMGUI_OUTPUT_LIB=%DN_CIMGUI_BUILD_DIR%\cimgui.lib"
  SET "DN_CIMGUI_OUTPUT_PDB=%DN_CIMGUI_BUILD_DIR%\cimgui.pdb"
  SET "DN_CIMGUI_INCLUDE_FLAGS=/I%DN_CIMGUI_DIR% /I%DN_CIMGUI_DIR%\imgui /I%DN_CIMGUI_DIR%\imgui\backends /I%DN_GLFW_DIR%\include"
  SET "DN_CIMGUI_OUTPUT_FLAGS=/Fe%DN_CIMGUI_OUTPUT_DLL% /Fo%DN_CIMGUI_OBJECT_DIR% /Fd%DN_CIMGUI_PDB_DIR%"
  SET "DN_CIMGUI_DEBUG_FLAGS=/Zi"
  SET "DN_CIMGUI_RUNTIME_FLAGS=/MDd"
  SET "DN_CIMGUI_LINKER_FLAGS=/link %DN_LIB_GLFW%"
  SET "DN_CIMGUI_PREPROCESSOR_FLAGS=/D IMGUI_IMPL_API="extern \"C\" __declspec(dllexport)""


  MKDIR %DN_CIMGUI_BUILD_DIR%
  MKDIR %DN_CIMGUI_INTERMEDIATE_DIR%

  @ECHO ON
  cl.exe /LD %DN_CIMGUI_OUTPUT_FLAGS% %DN_CIMGUI_SOURCES% %DN_CIMGUI_PREPROCESSOR_FLAGS% %DN_CIMGUI_INCLUDE_FLAGS% %DN_CIMGUI_DEBUG_FLAGS% %DN_CIMGUI_RUNTIME_FLAGS% %DN_CIMGUI_LINKER_FLAGS%
  @ECHO OFF

  CALL %DN_BUILD_TOOLS% :DN_ECHO_INFO "Copying CImGui binaries"
  CALL %DN_BUILD_TOOLS% :DN_REPLACE_FILE %DN_DLL_CIMGUI% %DN_CIMGUI_OUTPUT_DLL%
  CALL %DN_BUILD_TOOLS% :DN_REPLACE_FILE %DN_LIB_CIMGUI% %DN_CIMGUI_OUTPUT_LIB%
  CALL %DN_BUILD_TOOLS% :DN_REPLACE_FILE %DN_PDB_CIMGUI% %DN_CIMGUI_OUTPUT_PDB%

  CALL %DN_BUILD_TOOLS% :DN_ECHO_INFO "Copying CImGui headers"
  CALL %DN_BUILD_TOOLS% :DN_REPLACE_FILE %DN_CIMGUI_H% %DN_CIMGUI_H_OUTPUT%
  CALL %DN_BUILD_TOOLS% :DN_REPLACE_FILE %DN_CIMGUI_IMPL_H% %DN_CIMGUI_IMPL_H_OUTPUT%

  EXIT /B

:VS
  CALL %DN_BUILD_TOOLS% :DN_ECHO_TARGET "VS"

  CALL %DN_BUILD_TOOLS% :DN_INITIALIZE_VS_PROMPT %~1 %~2 %~3

  EXIT /B