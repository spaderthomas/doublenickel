@ECHO OFF

PROMPT $G$S

:: Import the DN build script, which defines any framework paths and provides some utilities
SET "DN_DIR=..\..\.."
SET "DN_BUILD_TOOLS=%DN_DIR%\build\dn.bat"
CALL %DN_BUILD_TOOLS%

:: Build absolute paths to various things in the project
SET "PROJECT=SdfClock"
SET "EXECUTABLE=%PROJECT%.exe"
CALL %DN_BUILD_TOOLS% :NORMALIZEPATH "%DN_DIR%" DN_DIR
CALL %DN_BUILD_TOOLS% :NORMALIZEPATH "%DN_BUILD_TOOLS%" DN_BUILD_TOOLS
CALL %DN_BUILD_TOOLS% :NORMALIZEPATH "%DN_DIR%\examples" DN_EXAMPLES_DIR
CALL %DN_BUILD_TOOLS% :NORMALIZEPATH "%DN_EXAMPLES_DIR%\sdf_clock" PROJECT_DIR
CALL %DN_BUILD_TOOLS% :NORMALIZEPATH "%PROJECT_DIR%\source" SOURCE_DIR
CALL %DN_BUILD_TOOLS% :NORMALIZEPATH "%SOURCE_DIR%\main.c" SOURCE_FILES
CALL %DN_BUILD_TOOLS% :NORMALIZEPATH "%PROJECT_DIR%\build" BUILD_DIR
CALL %DN_BUILD_TOOLS% :NORMALIZEPATH "%BUILD_DIR%\intermediate" INTERMEDIATE_DIR
CALL %DN_BUILD_TOOLS% :NORMALIZEPATH "%INTERMEDIATE_DIR%\" OBJECT_DIR
CALL %DN_BUILD_TOOLS% :NORMALIZEPATH "%INTERMEDIATE_DIR%\" PDB_DIR

CALL %DN_BUILD_TOOLS% :DN_ECHO_USER "DN: %DN_DIR%"
CALL %DN_BUILD_TOOLS% :DN_ECHO_USER "PROJECT: %PROJECT_DIR%"
CALL %DN_BUILD_TOOLS% :DN_ECHO_USER "BUILD: %BUILD_DIR%"

:: Call the correct target (or :BUILD by default)
IF NOT "%~1"=="" (SHIFT & GOTO %~1)

:BUILD
  CALL %DN_BUILD_TOOLS% :DN_ECHO_USER "Building %PROJECT%"

  :: Run pre-build tasks
  CALL %DN_BUILD_TOOLS% :DN_ECHO_USER "Creating intermediate directory at %INTERMEDIATE_DIR%"
  MKDIR %INTERMEDIATE_DIR%

  CALL %DN_BUILD_TOOLS% :DN_ECHO_USER "Copying DLLs to build directory..."
  CALL %DN_BUILD_TOOLS% :DN_COPY_DLLS %BUILD_DIR% 

  :: Initialize a developer prompt and call the compiler. DN_INITIALIZE_VS_PROMPT is merely for convenience; it doesn't do anything
  :: magic, just wraps a call to the standard batch scripts installed with VS.
  CALL %DN_BUILD_TOOLS% :DN_INITIALIZE_VS_PROMPT

  SET "OUTPUT_FLAGS=/Fe%EXECUTABLE% /Fo%OBJECT_DIR% /Fd%PDB_DIR%"
  SET "RUNTIME_FLAGS=/MDd"
  SET "EXTRA_FLAGS=/Od /JMC /EHa"
  SET "DEBUG_FLAGS=/Zi /Zc:wchar_t /Zc:forScope /Zc:inline"
  SET "LANGUAGE_FLAGS=/std:c11 /experimental:c11atomics /TC"
  SET "WARNING_FLAGS=/W3 /wd"4530" /wd"4201" /wd"4577" /wd"4310" /wd"4624" /wd"4099" /wd"4068" /wd"4267" /wd"4244" /wd"4018""
  SET "PREPROCESSOR_DEFINES=/D "DN_EDITOR" /D "_CRT_SECURE_NO_WARNINGS" /D "_SILENCE_CXX17_ALL_DEPRECATION_WARNINGS""
  SET "LINKER_FLAGS=/link /LIBPATH:%DN_LIB% /DEBUG:FULL /MACHINE:X64 /NOLOGO /SUBSYSTEM:CONSOLE /INCREMENTAL:NO /NOIMPLIB /NOEXP /PDB:%PDB_DIR%\%PROJECT%.pdb /ignore:4099 /ignore:4204"
  SET "SYSTEM_LIBRARIES="user32.lib" "opengl32.lib" "gdi32.lib" "Shell32.lib" "Kernel32.lib" "Advapi32.lib" "Ole32.lib" "OleAut32.lib""

  @ECHO ON
  cl.exe %OUTPUT_FLAGS% %SOURCE_FILES% %DN_SWITCH_INCLUDE% %RUNTIME_FLAGS% %LANGUAGE_FLAGS% %DEBUG_FLAGS% %EXTRA_FLAGS% %WARNING_FLAGS% %PREPROCESSOR_DEFINES% %LINKER_FLAGS% %DN_SWITCH_LIBS% %SYSTEM_LIBRARIES%
  @ECHO OFF

  EXIT /B

:CLEAN
  CALL %DN_BUILD_TOOLS% :DN_ECHO_USER "Cleaning project"
  RMDIR /s /q %INTERMEDIATE_DIR%
  DEL "%BUILD_DIR%\*.dll"
  DEL "%BUILD_DIR%\*.exe"
  EXIT /B

:FFI
  CALL %DN_BUILD_TOOLS% :DN_BUILD_FFI

  EXIT /B
