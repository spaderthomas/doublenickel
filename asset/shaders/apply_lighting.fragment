#include "common.glsl"
#include "light.glsl"

uniform sampler2D light_map;
uniform sampler2D color_buffer;
uniform sampler2D normal_buffer;
uniform sampler2D editor;

in vec4 f_color;
in vec2 f_uv;

out vec4 color;


void main () {

    vec2 position = gl_FragCoord.xy + camera;
    // vec2 position = banded_position;


    vec4 sample_color = texture(color_buffer, f_uv);
    vec3 base_color = sample_color.rgb * sample_color.a;

    vec3 normal = texture(normal_buffer, f_uv).rgb;
    normal.xy = normal.xy * 2.0 - 1.0;

    vec3 lit_color = vec3(0.0);

    Light global_light;
    // global_light.intensity = 0.5;
    global_light.intensity = 0.025;
    global_light.color = white;
    lit_color += base_color.rgb * global_light.color.rgb * global_light.intensity;

    for (int i = 0; i < num_lights; i++) {
        Light light = lights[i];

        vec2 uv = band_light_uvs(light, f_uv, position);
        vec2 fragment_position = uv * native_resolution + camera;;

        vec2 light_direction = normalize(light.position - fragment_position);

        float radial_falloff = calc_radial_falloff(light, fragment_position);
        float normal_falloff = clamp(dot(light_direction, normal.xy), 0.0, 1.0) * (normal.z);
        float angular_falloff = calc_angular_falloff(light, fragment_position);
        float light_strength = normal_falloff * light.intensity * radial_falloff * angular_falloff;

        lit_color += base_color * light.color.rgb * light_strength;

        lit_color += light.color.rgb * light.intensity * radial_falloff * light.volumetric_intensity * angular_falloff;
    }

    color = vec4(lit_color, 1.0);
}
 